println """
*******************************************
 You are building SKCraft Launcher! 

 Output files will be in [subproject]/build/libs
*******************************************
"""


buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.0'
    }
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyMMddHHmmss')
    return formattedDate
}

version = '4.3.'+getDate()

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'

    group = 'com.skcraft'
    version = rootProject.version

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    repositories {
        mavenCentral()
        maven { url "http://repo.maven.apache.org/maven2" }
    }

    if (JavaVersion.current().isJava8Compatible()) {
        // Java 8 turns on doclint which we fail
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

task createJson(type: Copy) {
    from 'launcher.json'
    into 'upload'
    
    expand(version: project.version)
}

task packLauncher(type:Exec, dependsOn: [':createJson']) {
    workingDir '.'
    executable 'pack200'
    args '--no-gzip', "upload/${project.version}.jar.pack", "launcher-fancy/build/libs/launcher-fancy-${project.version}-all.jar" 
}
